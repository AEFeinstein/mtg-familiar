apply plugin: 'com.android.application'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

//check.dependsOn 'pmd', /*'checkstyle',*/ 'findbugs'

repositories {
    mavenCentral()
    //google() // Gradle 4.0 adds this
}

def configDir = "${project.rootDir}/config"

task checkstyle(type: Checkstyle, group: "verification") {
    configFile file("$configDir/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("$configDir/checkstyle/suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

task findbugs(type: FindBugs, dependsOn: "assembleDebug", group: "verification") {
    ignoreFailures = false
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("$configDir/findbugs/findbugs.filter.xml")
    classes = files("${project.rootDir}/mobile/build/intermediates/classes")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    reports {
        xml.enabled = false
        html.enabled = true
        html {
            destination "$project.buildDir/reports/findbugs.html"
        }
    }
    classpath = files()
}

task pmd(type: Pmd, group: "verification") {
    ignoreFailures = true
    ruleSetFiles = files("$configDir/pmd/pmd.xml")
    reports {
        html.enabled = true
        html {
            destination "$project.buildDir/reports/pmd.html"
        }
    }
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.gelakinetic.mtgfam"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 50
        versionName "3.4.3"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.txt'
            //findbugs
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }
    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }
}

dependencies {
    androidTestCompile 'com.android.support:support-annotations:26.1.0'
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test:rules:1.0.1'
    // Optional -- Hamcrest library
    androidTestCompile 'org.hamcrest:hamcrest-library:1.4-atlassian-1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.8.47'
    compile 'org.apache.commons:commons-lang3:3.6'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'org.apache.commons:commons-collections4:4.1'
    compile 'cz.msebera.android:httpclient:4.4.1.2'
    compile 'com.octo.android.robospice:robospice:1.4.14', {
        exclude module: 'commons-lang3'
        exclude module: 'commons-io'
    }
    compile 'com.octo.android.robospice:robospice-cache:1.4.14', {
        exclude module: 'commons-lang3'
        exclude module: 'commons-io'
    }
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:support-v13:26.1.0'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:gridlayout-v7:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.github.woxthebox:draglistview:1.5.0', {
        exclude module: 'recyclerview-v7'
    }
    compile 'org.jetbrains:annotations:13.0'
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'com.afollestad.material-dialogs:core:0.9.5.0', {
        exclude module: 'support-v13'
        exclude module: 'appcompat-v7'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-annotations'
    }
    compile 'com.jpardogo.materialtabstrip:library:1.1.1', {
        exclude module: 'support-core-ui'
    }
    compile 'com.code-troopers.betterpickers:library:3.1.0', {
        exclude module: 'appcompat-v7'
    }
    compile 'com.github.machinarius:preferencefragment:0.1.2', {
        exclude module: 'support-v4'
    }
    compile 'org.jsoup:jsoup:1.10.3'
//    compile 'com.splitwise:tokenautocomplete:2.0.8'
    compile 'com.google.code.findbugs:annotations:3.0.1', {
        // Need to exclude these, or build is broken by:
        //   com.android.dex.DexException: Multiple dex files define Ljavax/annotation/CheckForNull
        exclude module: 'jsr305'
        exclude module: 'jcip-annotations'
    }
}

if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            maxParallelForks = 2
            minHeapSize = '128m'
        }
    }
}
