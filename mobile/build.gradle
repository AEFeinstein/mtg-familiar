/*
 * Copyright 2017 Adam Feinstein
 *
 * This file is part of MTG Familiar.
 *
 * MTG Familiar is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * MTG Familiar is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with MTG Familiar.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

check.dependsOn 'pmd', 'checkstyle', 'findbugs'

repositories {
    mavenCentral()
    google()
}

def configDir = "${project.rootDir}/config"

task checkstyle(type: Checkstyle, group: "verification") {
    ignoreFailures = true
    configFile file("$configDir/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("$configDir/checkstyle/suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

task findbugs(type: FindBugs, dependsOn: "assembleDebug", group: "verification") {
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("$configDir/findbugs/findbugs.filter.xml")
    classes = files("${project.rootDir}/mobile/build/intermediates/classes")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    reports {
        xml.enabled = false
        html.enabled = true
        html {
            destination "$project.buildDir/reports/findbugs.html"
        }
    }
    classpath = files()
}

task pmd(type: Pmd, group: "verification") {
    ignoreFailures = true
    ruleSetFiles = files("$configDir/pmd/pmd.xml")
    reports {
        html.enabled = true
        html {
            destination "$project.buildDir/reports/pmd.html"
        }
    }
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.gelakinetic.mtgfam"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 53
        versionName "3.4.6"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.txt'
            //findbugs
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }
    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }
}

dependencies {
    androidTestImplementation 'com.android.support:support-annotations:26.1.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    // Optional -- Hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.4-atlassian-1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.47'
    implementation 'org.apache.commons:commons-lang3:3.6'
    implementation 'org.apache.commons:commons-io:1.3.2', {
        exclude module: 'commons-io'
    }
    implementation 'org.apache.commons:commons-collections4:4.1'
    implementation 'cz.msebera.android:httpclient:4.4.1.2'
    implementation 'com.octo.android.robospice:robospice:1.4.14', {
        exclude module: 'commons-lang3'
        exclude module: 'commons-io'
    }
    implementation 'com.octo.android.robospice:robospice-cache:1.4.14', {
        exclude module: 'commons-lang3'
        exclude module: 'commons-io'
    }
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:support-v13:26.1.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:gridlayout-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.github.woxthebox:draglistview:1.5.0', {
        exclude module: 'recyclerview-v7'
    }
    implementation 'org.jetbrains:annotations:13.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.afollestad.material-dialogs:core:0.9.5.0', {
        exclude module: 'support-v13'
        exclude module: 'appcompat-v7'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-annotations'
    }
    implementation 'com.jpardogo.materialtabstrip:library:1.1.1', {
        exclude module: 'support-core-ui'
    }
    implementation 'com.code-troopers.betterpickers:library:3.1.0', {
        exclude module: 'appcompat-v7'
    }
    implementation 'com.github.machinarius:preferencefragment:0.1.2', {
        exclude module: 'support-v4'
    }
    implementation 'org.jsoup:jsoup:1.10.3'
//    implementation 'com.splitwise:tokenautocomplete:2.0.8'
    implementation 'com.google.code.findbugs:annotations:3.0.1', {
        // Need to exclude these, or build is broken by:
        //   com.android.dex.DexException: Multiple dex files define Ljavax/annotation/CheckForNull
        exclude module: 'jsr305'
        exclude module: 'jcip-annotations'
    }
}

if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            maxParallelForks = 2
            minHeapSize = '128m'
        }
    }
}
